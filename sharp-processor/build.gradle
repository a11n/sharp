apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

version = '0.2.0'

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {
  compile project (':sharp-api')
  compile 'com.squareup:javapoet:1.2.0'

  testCompile 'junit:junit:4.12'
  testCompile 'com.google.testing.compile:compile-testing:0.7'
  testCompile 'com.google.android:android:4.1.1.4'
}

jacocoTestReport {
  reports {
    xml.enabled = true // coveralls plugin depends on xml format report
    html.enabled = true
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  classifier = 'sources'
  from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  classifier = 'javadoc'
  from javadoc.destinationDir
}

artifacts {
  archives sourcesJar, javadocJar
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
      artifact sourcesJar
      artifact javadocJar
      groupId 'de.ad'
      artifactId 'sharp-processor'
      version project.version
    }
  }
}

bintray {
  user = project.hasProperty('bintray_user') ? bintray_user : ''
  key = project.hasProperty('bintray_key') ? bintray_key : ''
  publications = ['mavenJava']
  dryRun = false
  publish = false
  pkg {
    repo = 'maven'
    name = 'de.ad:sharp-processor'
    desc = 'An annotation based SharedPreferences wrapper.'
    websiteUrl = 'https://github.com/a11n/sharp'
    issueTrackerUrl = 'https://github.com/a11n/sharp/issues'
    vcsUrl = 'https://github.com/a11n/sharp.git'
    licenses = ['MIT']
    labels = ['Android', 'SharedPreferences', 'Annotation', 'Annotation Processor']
    publicDownloadNumbers = false
    version {
      name = project.version
      desc = ''
    }
  }
}